apply plugin: 'com.android.application'

def leanCloud = rootProject.ext.leanCloud
def GDTAdId = rootProject.ext.GDTAdId

android {
    compileSdkVersion 26

    defaultConfig {
        applicationId "com.tf.transfer"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 5
        versionName "2.0.3"
        flavorDimensions "release"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }

    }

    buildTypes.each {
        it.buildConfigField("String", "leanCloudApplicationId", leanCloud.applicationId)
        it.buildConfigField("String", "leanCloudClientKey", leanCloud.clientKey)
        it.buildConfigField("String", "GDTAdIdAppId", GDTAdId.appId)
        it.buildConfigField("String", "GDTAdIdhomeBanner", GDTAdId.homeBanner)
        it.buildConfigField("String", "GDTAdIdFileExplorerInterstitial", GDTAdId.fileExplorerInterstitial)
        it.buildConfigField("String", "GDTAdIdExitAppNative", GDTAdId.exitAppNative)
    }

    productFlavors {
        yingyongbao {}
        baidu {}
        huawei {}
        xiaomi {}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [LEANCLOUD_CHANNEL_VALUE: name]
    }

    // 自定义打包时apk名称
    applicationVariants.all { variant ->
        variant.outputs.all { output ->// each 改为 all
            def fileName = "TF快传_${variant.versionName}_release.apk"
            def outFile = output.outputFile
            if (outFile != null && outFile.name.endsWith('.apk')) {
                outputFileName = fileName  //  output.outputFile 改为 outputFileName
            }
        }
    }
}

dependencies {
    implementation files('libs/avoscloud-sdk-v3.16.jar')
    implementation files('libs/avoscloud-statistics-v3.16.jar')
    implementation files('libs/core-2.3-SNAPSHOT.jar')
    implementation files('libs/fastjson.jar')
    implementation files('libs/httpmime-4.2.4.jar')
    implementation files('libs/javase-2.3-SNAPSHOT.jar')
    implementation files('libs/nineoldandroids-2.4.0.jar')
    implementation files('libs/okhttp-2.6.0-leancloud.jar')
    implementation files('libs/okio-1.6.0-leancloud.jar')
    implementation files('libs/GDTUnionSDK.TBS.4.20.580.min.jar')

    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.hwangjr.rxbus:rxbus:1.0.6'
}
